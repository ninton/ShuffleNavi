apply plugin: 'com.android.application'

android {
    signingConfigs
            {
        release {
            keyAlias System.getenv('keyAlias')
            keyPassword System.getenv('keyPassword')
            storeFile file("${System.getenv('storeFile')}")
            storePassword System.getenv('storePassword')
        }
    }

    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "jp.co.ninton.shufflenavi"
        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "flurry_api_key", "\"" + System.getenv("flurry_api_key") + "\""
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "String", "flurry_api_key", "\"" + System.getenv("flurry_api_key") + "\""
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            variant.outputs.each { output ->
                if (output.outputFileName != null && output.outputFileName.endsWith('.apk')) {
                    // Rename APK
                    def applicationId = defaultConfig.applicationId
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def date = new java.text.SimpleDateFormat("yyyy-MM-dd-HH-mm-ss").format(new Date())
                    def newName = "${applicationId}_r${versionCode}_v${versionName}_${date}.apk"
                    output.outputFileName = newName
                }
            }
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'RtlEnabled'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:support-v4:24.4.0'

    implementation 'com.flurry.android:analytics:11.6.0'
}
